#!/usr/bin/osascript

property frontmostSheet : null

on run argv
	set theApp to item 1 of argv
	set isValidDialog to false
	set buttonNames to {system attribute "localize_open", system attribute "localize_save", system attribute "localize_export", system attribute "localize_move", system attribute "localize_select"}
	tell application "System Events"
		tell application process theApp
			repeat with aWindow in windows
				set theElement to my getFrontmostSheet(theApp, aWindow)
				if theElement is not null then
					set isValidDialog to my checkButtons(theElement, buttonNames)
				else
					set theElement to aWindow
					set isValidDialog to my checkButtons(theElement, buttonNames)
				end if
				if isValidDialog is true then
					perform action "AXRaise" of theElement
					delay 0.1
					exit repeat
				end if
			end repeat
		end tell
	end tell
	return isValidDialog
end run

on getFrontmostSheet(a, el)
	tell application "System Events"
		try
			set elCheck to sheet 1 of el
			set frontmostSheet to elCheck
			my getFrontmostSheet(a, elCheck)
		on error theError
		end try
		return frontmostSheet
	end tell
end getFrontmostSheet

on checkButtons(element, btnNames)
	set checkButtonsResult to false
	tell application "System Events"
		repeat with aButton in buttons of element
			if btnNames contains name of aButton then
				set checkButtonsResult to true
				exit repeat
			end if
		end repeat
	end tell
	return checkButtonsResult
end checkButtons
